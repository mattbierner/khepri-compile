/**
 * @fileOverview Rewrite normalized Khepri to ECMAScript.
 */
package (
    transform)
with     
    import 'akh::trans::state' StateT,
    import 'akh::unique' Unique,
    import 'akh::base' {
        liftM2
        'sequence': seq,
        sequencea},
    
    import 'zipper-m::trans::tree' TreeZipperT,
    
    import 'ecma-ast-zipper' {ecmaZipper},
    
    import '../ast' {
        type
        getUd
        setUd
        getUid
        setUid
        getLocals
        getClosure},
    import '../lexical/scope' scope#{Scope},
    import '../fun' {
        concat
        flatten
        flip
        foldr
        map},
    import '../builtin' {builtins},
    import './unpack' {
        expandBindings},
    import './state' state#{State},
    import './translation' translate,
    
    import './package_manager/amd' _,
    import './package_manager/node' _
in {

/* Forward
 ******************************************************************************/
var _trans;

/* Monad
 ******************************************************************************/
var M := TreeZipperT (StateT Unique); // HACK: prevent call stack explode

var run = \m s ctx ->
    Unique.runUnique(
        StateT.evalStateT(
            TreeZipperT.runTreeZipperT(m, ctx),
            s));

var pass := M.of null;

var cons := liftM2 @ concat;

var enumeration := foldr@(flip cons, M.of []);

/* Operations
 ******************************************************************************/
// Zipper Operations
////////////////////////////////////////
var node = M.node;

var withNode = M.chain @ node;

var modify = M.modifyNode;
var set = M.setNode;

var up = M.up;
var down = M.down;
var right = M.right;

var moveChild = M.child;

var child := \edge ...actions ->
    seq(
        moveChild edge,
        sequencea actions,
        up);

var checkTop := node.chain \ x -> _trans x; // forward

var rewriteChild := \edge ->
    child(edge,
        checkTop);

// State Operations
////////////////////////////////////////
var extract = M.lift (M.inner.get);

var setState = M.lift <\ M.inner.put;

var modifyState = M.lift <\ M.inner.modify;

var inspectStateWith = M.chain @ extract;

var packageManager = extract.map (.packageManager);

// Scoping Operations
////////////////////////////////////////
var inspectScope = \f -> extract.map (f <\ (.scope));

var modifyScope = \f ->
    modifyState \ s ->
        s.setScope(f s.scope);

/**
 * Enter a new real scope.
 */
var enterBlock = modifyScope (scope.push);

/**
 * Exit the current real scope.
 */
var exitBlock = modifyScope (scope.pop);

/**
 * Perform `actions` in an new real block.
 */
var realBlock := \-actions(...)->
    seq(
        enterBlock,
        sequencea actions,
        exitBlock);

/**
 * Get the identifier name for `uid`.
 */
var getMapping := \uid ->
    inspectScope (scope.getMapping @ uid);

/**
 * Add an new identifier in the current scope.
 * 
 * @param id Source id name.
 * @param uid Unique id.
 */
var addVar := \id uid ->
    modifyScope (scope.addVar@(id, uid));

// Ctx Operations
////////////////////////////////////////
var getContext = extract.map(.ctx);

var setContext = \ctx ->
    modifyState \ s ->
        s.setCtx(ctx);

var ctxBlock := \newCtx ->
    \-actions(...)->
        getContext.chain \ ctx ->
            seq(
                setContext newCtx,
                sequencea actions,
                setContext ctx);

var loopBlock := ctxBlock (state.LOOP_CTX);
var funcBlock := ctxBlock (state.NORMAL_CTX);

// Bindings Operations
////////////////////////////////////////
var pushBindings = modifyState (state.pushBindings);

var popBindings = modifyState (state.popBindings);

/**
 * Get all bindings in the current scope.
 */
var getBindings = M.chain @ inspectStateWith(
    enumeration
        <\ map @ getMapping
        <\ state.getBindings);

/**
 * 
 */
var addBindings = \bindings ->
    modifyState (state.addBindings @ bindings);

/**
 * 
 */
var addBindingsForBindingsList :=
    map @ (.pattern \> expandBindings@null)
    \> flatten
    \> map @ (.pattern.id \> getUid)
    \> addBindings;

/* Transforms
 ******************************************************************************/
var transformers = {};

var addTransform = \type check -> {
    if (Array.isArray type)
        type.forEach \ x -> addTransform(x, check);
    else
        transformers.(type) = check;
};

// Declarations
addTransform@'VariableDeclaration' <| seq(
    rewriteChild 'declarations',
    modify (translate.variableDeclaration));

addTransform@'Binding' <| seq(
    child('pattern',
        rewriteChild 'id'),
    rewriteChild 'value');

addTransform@'VariableDeclarator' <| seq(
    rewriteChild 'id',
    rewriteChild 'init',
    modify (translate.variableDeclarator));

addTransform@'StaticDeclaration' <|
    modify (translate.emptyStatement);

// Clauses
addTransform@'CatchClause' <| seq(
    rewriteChild 'param',
    rewriteChild 'body',
    modify (translate.catchClause));

addTransform@'SwitchCase' <| seq(
    rewriteChild 'test',
    rewriteChild 'consequent',
    modify (translate.switchCase));

// Statement
addTransform@'BlockStatement' <| seq(
    pushBindings,
    rewriteChild 'body',
    getBindings \ bindings ->
        modify (translate.blockStatement @ bindings),
    popBindings);

addTransform@'ExpressionStatement' <| seq(
    rewriteChild 'expression',
    modify (translate.expressionStatement));

addTransform@'IfStatement' <| seq(
    rewriteChild 'test',
    rewriteChild 'consequent',
    rewriteChild 'alternate',
    modify (translate.ifStatement));

addTransform@'WithStatement' <| seq(
    rewriteChild 'bindings',
    rewriteChild 'body',
    modify (translate.withStatement));

addTransform@'SwitchStatement' <| seq(
    rewriteChild 'discriminant',
    rewriteChild 'cases',
    modify (translate.switchStatement));

addTransform@'ReturnStatement' <| seq(
    rewriteChild 'argument',
    modify (translate.returnStatement));

addTransform@'ThrowStatement' <| seq(
    rewriteChild 'argument',
    modify (translate.throwStatement));

addTransform@'BreakStatement' <|
    modify (translate.breakStatement);

addTransform@'ContinueStatement' <|
    modify (translate.continueStatement);

addTransform@'TryStatement' <| seq(
    rewriteChild 'block',
    rewriteChild 'handler',
    rewriteChild 'finalizer',
    modify (translate.tryStatement));

addTransform@'WhileStatement' <| seq(
    rewriteChild 'test',
    loopBlock(
        rewriteChild 'body'),
    modify (translate.whileStatement));

addTransform@'DoWhileStatement' <| seq(
    loopBlock(
        rewriteChild 'body'),
    rewriteChild 'test',
    modify (translate.doWhileStatement));

addTransform@'ForStatement' <| seq(
    rewriteChild 'init',
    rewriteChild 'test',
    rewriteChild 'update',
    loopBlock(
        rewriteChild 'body'),
    modify (translate.forStatement));

// Expression
addTransform@'AssignmentExpression' <| seq(
    rewriteChild 'left',
    rewriteChild 'right',
    modify (translate.assignmentExpression));

addTransform@'UnaryExpression' <| seq(
    rewriteChild 'argument',
    modify (translate.unaryExpression));

addTransform@'BinaryExpression' <| seq(
    rewriteChild 'operator',
    rewriteChild 'left',
    rewriteChild 'right',
    modify (translate.binaryExpression));

addTransform@'ConditionalExpression' <| seq(
    rewriteChild 'test',
    rewriteChild 'consequent',
    rewriteChild 'alternate',
    modify (translate.conditionalExpression));

addTransform@'NewExpression' <| seq(
    rewriteChild 'callee',
    rewriteChild 'args',
    modify (translate.newExpression));

addTransform@'CallExpression' <| seq(
    rewriteChild 'callee',
    rewriteChild 'args',
    modify (translate.callExpression));

addTransform@'ApplyExpression' <| seq(
    rewriteChild 'callee',
    rewriteChild 'args',
    modify (translate.applyExpression));

addTransform@'MemberExpression' <| seq(
    rewriteChild 'object',
    rewriteChild 'property',
    modify (translate.memberExpression));

addTransform@'CheckedMemberExpression' <| seq(
    rewriteChild 'object',
    rewriteChild 'property',
    modify (translate.checkedMemberExpression),
    checkTop);

addTransform@'LetExpression' <| seq(
    rewriteChild 'bindings',
    withNode (
        (.bindings) \> addBindingsForBindingsList),
    rewriteChild 'body',
    modify (translate.letExpression));

addTransform@'CurryExpression' <| seq(
    rewriteChild 'base',
    rewriteChild 'args',
    modify (translate.curryExpression));

var createExplicitClosure = \locals ->
    enumeration(
        map(
            \uid ->
                getMapping(uid).map(\name ->
                    translate.identifier(node.loc, name, uid)),
            locals))
        .chain \ locals ->
            modify \ node ->
                translate.explicitClosure(locals, node);

//TODO: remove duplicate translate stage
addTransform@'FunctionExpression' <| let
    evaluate = funcBlock <|
    realBlock(
        rewriteChild 'id',
        modify \ node ->
            translate.functionExpression(node.loc,
                node.id,
                node.params,
                node.body,
                getUd('prefix', node)),
        
        rewriteChild 'params',
        rewriteChild 'body',
        
        modify (translate.functionExpressionPost))
in
    getContext.chain \ ctx ->
        ?ctx === state.LOOP_CTX
            :withNode \ node -> seq(
                evaluate,
                createExplicitClosure(getClosure node))
            :evaluate;

addTransform@'ArrayExpression' <| seq(
    rewriteChild 'elements',
    modify (translate.arrayExpression));

addTransform@'ObjectExpression' <| seq(
    rewriteChild 'properties',
    modify (translate.objectExpression));

addTransform@'ObjectValue' <| seq(
    rewriteChild 'key',
    rewriteChild 'value',
    modify (translate.objectValue));

// Patterns
addTransform@'IdentifierPattern' <| seq(
    rewriteChild 'id',
    modify (.id));

addTransform@['RelativeUnpack', 'SliceUnpack'] <|
    rewriteChild 'target';

addTransform@'ArgumentsPattern' <| seq(
    rewriteChild 'id',
    rewriteChild 'elements',
    rewriteChild 'self');

// Program
addTransform@'Program' <| seq(
    pushBindings,
    
    // Add 'use strict' header to non-package programs.
    modify \ node ->
        ?type node.body === 'Package'
            :node
            :setUd('prefix', translate.useStrict, node),
    
    rewriteChild 'body',
    getBindings \ bindings ->
        modify (translate.program @ bindings));

// Package
addTransform@'Package' <| seq(
    packageManager.chain \ packageManager ->
        modify (translate.packageBlock @Â packageManager),
    
    // the package managers are expressed in khepri code so we have to translate the output
    checkTop); 

addTransform@'Import' <|
    packageManager.chain \ packageManager ->
        modify ((.from) \> packageManager.importPackage);

// Value
addTransform@'Identifier' <|
    withNode \ node ->
        ?getUid node
            // When node has uid, get mapped name for uid.
            :seq(
                addVar(node.name, getUid node),
                getMapping(getUid node).chain \ name ->
                    set <| translate.identifier(node.loc, name, getUid node))
        
        // When node has no uid, don't rename
        :set <| translate.identifier(node.loc, node.name);

/*
 *****************************************************************************/
/**
 * Visit an array node.
 */
var visitArray := let
    move = \x i a ->
        ?i === a.length - 1
            :checkTop
            :seq(checkTop, right)
in
    \node ->
        ?node.length
            :seq(
                down,
                sequencea map(move, node),
                up)
        :pass;

/**
 * 
 */
_trans := \node ->
    ?Array.isArray node
        :visitArray node
        :(transformers.(type node) || pass);

var transformProgram := seq(
    addVar('require', getUid(builtins.require)),
    addVar('exports', getUid(builtins.exports)),
    checkTop,
    node.map (ecmaZipper));

/* API
 *****************************************************************************/
var getPackageManager = \manager -> {
    with 
        import './package_manager/amd' amd_manager,
        import './package_manager/node' node_manager
    in {
        return ?manager === 'node' : node_manager : amd_manager;
    }
};

/**
 * Rewrite a Khepri zippered program to an ECMAScript program.
 * 
 * Outputs a zippered ECMAScript AST.
 * 
 * @param ast Zippered khepri program.
 * @param manager Package manager to use for rewrite.
 */
transform = \ast manager -> let
    packageManager = getPackageManager manager,

    s = State.empty
        .setPackageManager(packageManager)
in
    run(transformProgram, s, ast);

}